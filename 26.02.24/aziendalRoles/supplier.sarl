package io.sarl.test1

import io.sarl.api.core.Behaviors
import io.sarl.api.core.DefaultContextInteractions
import io.sarl.api.core.ExternalContextAccess
import io.sarl.api.core.Initialize
import io.sarl.api.core.Lifecycle
import io.sarl.api.core.Logging
import io.sarl.api.core.OpenEventSpace
import io.sarl.api.core.OpenEventSpaceSpecification
import io.sarl.api.core.ParticipantJoined
import java.util.UUID
import java.util.LinkedList
import io.sarl.api.core.Schedules

agent SupplierAgent {

	uses Lifecycle, DefaultContextInteractions, Logging, Behaviors, ExternalContextAccess, Schedules
	
	uses StandardCap

	var comspace : OpenEventSpace
	var sem = new Object
	var executed = false
	var nStudents : Number = 0
	var speed : long = 0
	var probability : int = 0

	on Initialize {
		loggingName = occurrence.parameters.get(1) as String
		
		/*info("Supplier is operative")*/

		comspace = defaultContext.getOrCreateSpaceWithID(typeof(OpenEventSpaceSpecification), 
			occurrence.parameters.get(0) as UUID)

		comspace.registerStrongParticipant(asEventListener())
		
		//info(comspace.spaceID.ID)
	}
	
	on NewStats {
		info("Changing my stats")
		
		var plan = occurrence.plan
		nStudents = plan.NDoc
		speed = plan.speed
		probability = plan.probability
		
		var evt = new SupplierReady()
		evt.source = comspace.getAddress(getID())
		comspace.emit(evt)
		wake(evt)
		info("Ready")
	}

	on Hello [!isFromMe] {
		info("Hello!")
	}

	on Die {
		/*info("Asked to die")*/
		comspace.unregister(asEventListener())
		killMe
	}

	on ParticipantJoined [(!isFromMe) && (occurrence.spaceID == comspace.spaceID)] {
		/*info("Someone came to class")*/
		synchronized (sem){
			if (nStudents != 0 && comspace.numberOfWeakParticipants == nStudents && !executed) {
				info("All of the students are in")
				
				// teach skills
				var learn = new LearnSkills(probability)
				learn.source = comspace.getAddress(getID())
				comspace.emit(learn)
				
				executed = true
				in(speed) [
					var leave = new LeaveDepartment(comspace, true)
					leave.source = comspace.getAddress(getID())
					comspace.emit(leave)
					executed = false
				]
			}
		}
	}
}
